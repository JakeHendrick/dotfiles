#!/usr/bin/env zsh

# Show info on the current git status and branch
git_info () {
  local gitBranch=$(git branch 2> /dev/null | sed -e "/^[^*]/d" -e "s/* \(.*\)/\1/")

  # proceed if we're in a git repository
  if [[ $gitBranch ]]; then
    local statusDot="●"
    local statusColor="white"
    local statusText=$(git status 2> /dev/null)
    if [[ $statusText =~ 'no changes added' ]]; then
      statusDot="○"
      statusColor="red"
    elif [[ $statusText =~ 'untracked files present' ]]; then
      statusDot="○"
      statusColor="red"
    elif [[ $statusText =~ 'Changes to be committed' ]]; then
      statusDot="◍"
      statusColor="yellow"
    elif [[ $statusText =~ 'Your branch is ahead' ]]; then
      statusDot="●"
      statusColor="cyan"
    elif [[ $statusText =~ 'working tree clean' ]]; then
      statusDot="●"
      statusColor="green"
    fi

    # print the branch symbol if on a non-default branch
    if [[ ! $gitBranch =~ (main|master) ]]; then
      echo -en "%F{242}⌥%f "

      # add the branch name if we have room
      if [[ $COLUMNS -gt 79 ]]; then
        echo -en "%F{247}$gitBranch%f "
      fi
    fi

    # add the status dot
    echo -en "%F{"$statusColor"}$statusDot%f "
  fi
}
